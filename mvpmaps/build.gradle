apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 3
        versionName "0.3"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    testImplementation 'junit:junit:4.12'

    api 'com.benoitletondor:mvp-core:2.0'

    implementation 'androidx.appcompat:appcompat:1.0.2'

    api 'com.google.android.gms:play-services-location:17.0.0'
    api 'com.google.android.gms:play-services-maps:17.0.0'
}

// Bintray
archivesBaseName = "mvp-maps"
group = "com.benoitletondor"
version = project.android.defaultConfig.versionName

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator)) + configurations.compile
}

afterEvaluate {
    javadoc.classpath += files(android.libraryVariants.collect { variant ->
        variant.javaCompile.classpath.files
    })
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

install {
    repositories.mavenInstaller {
        pom.project {
            group "com.benoitletondor"
            artifactId "mvp-maps"
            packaging 'aar'
            version project.android.defaultConfig.versionName
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    // jFrog plugin must be declared for this line to work
    configurations = ['archives']
    // Package info for BinTray
    pkg {
        repo = 'maven'
        name = 'mvp-maps'
        userOrg = user
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/benoitletondor/mvp-maps.git'
        version {
            name = project.android.defaultConfig.versionName
            desc = 'MVP Maps'
            vcsTag = project.android.defaultConfig.versionName
        }
    }
}

artifacts {
    archives javadocJar
    archives sourcesJar
}
